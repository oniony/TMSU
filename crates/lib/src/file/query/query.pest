// Copyright 2011-2025 Paul Ruane.

// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU General Public License as published by
// the Free Software Foundation, either version 3 of the License, or
// (at your option) any later version.

// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU General Public License for more details.

// You should have received a copy of the GNU General Public License
// along with this program.  If not, see <http://www.gnu.org/licenses/>.

query = _{ SOI ~ maybe_or? ~ EOI }

tag   = @{ quoted_tag | unquoted_tag }
value = @{ quoted_value | unquoted_value }

or  = { maybe_and ~ ^"or" ~ maybe_or }
and = { maybe_not ~ (^"and"?) ~ maybe_and }
not = { ^"not" ~ operand }

equal            = { tag ~ ("==" | "=" | ^"eq") ~ value }
not_equal        = { tag ~ ("!=" | ^"ne") ~ value }
less_than        = { tag ~ ("<" | ^"lt") ~ value }
greater_than     = { tag ~ (">" | ^"gt") ~ value }
less_or_equal    = { tag ~ ("<=" | "≤" | ^"le") ~ value }
greater_or_equal = { tag ~ (">=" | "≥" | ^"ge") ~ value }

maybe_or    = _{ or | maybe_and }
maybe_and   = _{ and | maybe_not }
maybe_not   = _{ not | operand }
operand     = _{ parenthesis | comparison | tag }
parenthesis = _{ "(" ~ maybe_or ~ ")" }
comparison  = _{ equal | not_equal | less_than | greater_than | less_or_equal | greater_or_equal }

unquoted_tag   = _{ !(^"and" | ^"or" | ^"not") ~ unquoted_name_character+ }
quoted_tag     = _{ "\"" ~ quoted_name_character+ ~ "\"" }
unquoted_value = _{ unquoted_name_character+ }
quoted_value   = _{ "\"" ~ quoted_name_character+ ~ "\"" }

quoted_name_character     = _{ !("\"") ~ (LETTER | NUMBER | MARK | PUNCTUATION | SYMBOL | SEPARATOR) }
unquoted_name_character   = _{ !("=" | "<" | ">" | "≤" | "≥" | "(" | ")") ~ (LETTER | NUMBER | MARK | PUNCTUATION | SYMBOL) }

WHITESPACE = _{ " " }
